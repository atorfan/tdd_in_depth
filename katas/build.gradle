plugins {
    id 'application'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            platform('org.junit:junit-bom:5.9.1'),
            'org.junit.jupiter:junit-jupiter',
            'org.junit.jupiter:junit-jupiter-params',
            'io.cucumber:cucumber-java:7.14.0',
            'io.cucumber:cucumber-junit:7.14.0',
            'io.cucumber:cucumber-junit-platform-engine:7.14.0'
    )
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register('cucumber') {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber',
                    //'--plugin', 'json:target/cucumber/cucumber.json',
                    '--glue', 'com.codurance.atm',
                    'src/test/resources/feature'
            ]
        }
    }
}
